#!/usr/bin/perl

#- Copyright (C) 2000, 2001, 2002, 2003, 2004, 2005 MandrakeSoft SA
#- Copyright (C) 2005, 2006 Mandriva SA
#-
#- This program is free software; you can redistribute it and/or modify
#- it under the terms of the GNU General Public License as published by
#- the Free Software Foundation; either version 2, or (at your option)
#- any later version.
#-
#- This program is distributed in the hope that it will be useful,
#- but WITHOUT ANY WARRANTY; without even the implied warranty of
#- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#- GNU General Public License for more details.
#-
#- You should have received a copy of the GNU General Public License
#- along with this program; if not, write to the Free Software
#- Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.

#- this program is based upon old urpmi.addmedia

use strict;
use urpm;
use urpm::args 'options';
use urpm::msg;
use urpm::download ();

sub usage {
    print N("usage: urpmi.update [options] <name> ...
where <name> is a medium name to update.
") . N("  --help         - print this help message.
") . N("  --wget         - use wget to retrieve distant files.
") . N("  --curl         - use curl to retrieve distant files.
") . N("  --limit-rate   - limit the download speed.
") . N("  --proxy        - use specified HTTP proxy, the port number is assumed
                   to be 1080 by default (format is <proxyhost[:port]>).
") . N("  --proxy-user   - specify user and password to use for proxy
                   authentication (format is <user:password>).
") . N("  --update       - update only update media.
") . N("  --no-md5sum    - disable MD5SUM file checking.
") . N("  --force-key    - force update of gpg key.
") . N("  --norebuild    - don't try to rebuild hdlist if not readable.
") . N("  --ignore       - don't update, mark the media as ignored.
") . N("  --no-ignore    - don't update, mark the media as enabled.
") . N("  -a             - select all non-removable media.
") . N("  -c             - clean headers cache directory.
") . N("  -f             - force generation of hdlist files.
") . N("  -q             - quiet mode.
") . N("  -v             - verbose mode.
");
    exit 0;
}

$ENV{PATH} = "/sbin:/usr/sbin:/bin:/usr/bin:/usr/X11R6/bin";
delete @ENV{qw(ENV BASH_ENV IFS CDPATH)};

our @toupdates; #- set by urpm::args
my $urpm = new urpm;

$options{force} = 0;
$options{noclean} = $options{verbose} = 1;

urpm::args::parse_cmdline(urpm => $urpm) or exit(1);

$options{verbose} > 0 or $urpm->{log} = sub {};

if ($< != 0) {
    $urpm->{fatal}(1, N("Only superuser is allowed to update media"));
}
$urpm->read_config;
exists $options{limit_rate} or $options{limit_rate} = $urpm->{options}{'limit-rate'};

my @entries = map { $_->{name} } @{$urpm->{media}};

if ($options{all} && !defined $options{ignore}) {
    @entries == 0 and die N("nothing to update (use urpmi.addmedia to add a media)\n");
} else {
    if ($options{all}) { @toupdates = '' } #- select all
    $urpm->select_media(@toupdates);
    my $something_todo = 0;
    foreach (@{$urpm->{media}}) {
	$options{update} && $_->{update} and $_->{modified} = 1;
	if ($_->{modified}) {
	    if ($options{ignore}) {
		$_->{ignore} = 1;
	    } else {
		#- force ignored media to be returned alive.
		delete $_->{ignore};
	    }
	    ++$something_todo;
	}
    }

    $something_todo or die N("the entry to update is missing\n(one of %s)\n", join(", ", @entries));
}

if (defined $options{ignore}) {
    my $str = join(", ", map { N("\"%s\"", $_->{name}) } grep { $_->{modified} } @{$urpm->{media}});
    $urpm->{log}($options{ignore} ? N("ignoring media %s", $str) : N("enabling media %s", $str));
    $urpm->write_config;
} else {
    $urpm->update_media(%options, callback => \&urpm::download::sync_logger);
    #- try to umount removable device which may have been mounted.
    $urpm->try_umounting_removables;
}
